cmake_minimum_required(VERSION 3.7)
project(@PACKAGE_NAME@-Android-${ANDROID_ABI})

if(NCINE_DYNAMIC_LIBRARY)
	add_library(ncine SHARED IMPORTED)
	set_target_properties(ncine PROPERTIES
		IMPORTED_LOCATION "${CMAKE_SOURCE_DIR}/ncine/${ANDROID_ABI}/libncine.so"
		INTERFACE_INCLUDE_DIRECTORIES "${CMAKE_SOURCE_DIR}/ncine/include")
else()
	add_library(ncine STATIC IMPORTED)
	set_target_properties(ncine PROPERTIES
		IMPORTED_LOCATION "${CMAKE_SOURCE_DIR}/ncine/${ANDROID_ABI}/libncine.a"
		INTERFACE_INCLUDE_DIRECTORIES "${CMAKE_SOURCE_DIR}/ncine/include")
	target_compile_definitions(ncine INTERFACE "NCINE_STATIC")
endif()

add_library(ncine_main STATIC IMPORTED)
set_target_properties(ncine_main PROPERTIES
	IMPORTED_LOCATION "${CMAKE_SOURCE_DIR}/ncine/${ANDROID_ABI}/libncine_main.a"
	INTERFACE_INCLUDE_DIRECTORIES "${CMAKE_SOURCE_DIR}/ncine/include")

set(SOURCES
@ANDROID_PACKAGE_SOURCES@
)

add_library(native_app_glue STATIC ${ANDROID_NDK}/sources/android/native_app_glue/android_native_app_glue.c)
target_link_libraries(native_app_glue PRIVATE android log)
add_library(game SHARED ${SOURCES})
target_link_libraries(game PRIVATE native_app_glue ncine_main ncine)
target_include_directories(game PRIVATE ${ANDROID_NDK}/sources/android/native_app_glue)

# Convert strings back to the original lists
string(REPLACE " " ";" GENERATED_SOURCES "${GENERATED_SOURCES}")
string(REPLACE " " ";" ANDROID_GENERATED_FLAGS "${ANDROID_GENERATED_FLAGS}")

target_include_directories(game PRIVATE @ANDROID_INCLUDE_DIRS@)
target_sources(game PRIVATE ${GENERATED_SOURCES})
if(IS_DIRECTORY ${GENERATED_INCLUDE_DIR})
	target_include_directories(game PRIVATE ${GENERATED_INCLUDE_DIR})
endif()

if(NOT NCINE_DYNAMIC_LIBRARY)
	include(package_imported_targets)
	target_link_libraries(game PRIVATE
		${EGL_LIBRARY} ${GLES3_LIBRARY} ${OPENSLES_LIBRARY} ${ZLIB_LIBRARY}
		PNG::PNG WebP::WebP OpenAL::AL Vorbis::Vorbisfile Lua::Lua)
endif()

# Export ANativeActivity_onCreate() - https://github.com/android-ndk/ndk/issues/381
set_property(TARGET game APPEND_STRING PROPERTY LINK_FLAGS "-u ANativeActivity_onCreate")

include(package_strip_binaries)
